Q1.

PREFIX wod: <https://paris-saclay.fr/courses/wod/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?regionLabel (COUNT(?city) AS ?nbCities) where { 
    ?city rdf:type wod:Commune ;
       wod:inDepartment ?dep .
    ?dep rdf:type wod:Department ;
         wod:inRegion ?reg .
    ?reg rdf:type wod:Region ;
         rdfs:label ?regionLabel
} GROUP BY ?regionLabel
ORDER BY DESC(?nbCities)


--------

Q2.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wod: <https://paris-saclay.fr/courses/wod/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>
select ?neighborLabel ?distance where { 
    ?orsay rdf:type wod:Commune ;
        rdfs:label "Orsay"@fr ;
        geo:hasGeometry ?geoOrsay . 
    ?geoOrsay geo:asWKT ?coordinatesOrsay .
    
    ?neighbor rdf:type wod:Commune ;
              rdfs:label ?neighborLabel ;
        geo:hasGeometry ?geoNeighbor . 
    ?geoNeighbor geo:asWKT ?coordinatesNeighbor .
    
    BIND(geof:distance(?coordinatesOrsay, ?coordinatesNeighbor, uom:metre) AS ?distance)
    FILTER(?neighbor != ?orsay)
} ORDER BY ?distance 
LIMIT 10


--------

Q3.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wod: <https://paris-saclay.fr/courses/wod/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>
select ?city1Label ?neighborLabel ?distance where { 
    ?dep rdf:type wod:Department ;
         rdfs:label "Essonne"@fr .
    
    ?city1 rdf:type wod:Commune ;
        rdfs:label ?city1Label ;
        wod:inDepartment ?dep ;
        geo:hasGeometry ?geoCity1 . 
    ?geoCity1 geo:asWKT ?coordinatesCity1 .
    
    ?neighbor rdf:type wod:Commune ;
              rdfs:label ?neighborLabel ;
              wod:inDepartment ?dep ;
        geo:hasGeometry ?geoNeighbor . 
    ?geoNeighbor geo:asWKT ?coordinatesNeighbor .
    
    BIND(geof:distance(?coordinatesCity1, ?coordinatesNeighbor, uom:metre) AS ?distance)
    FILTER(?neighbor != ?city1)
} ORDER BY DESC(?distance) 
LIMIT 1


